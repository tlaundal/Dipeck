class NodePluginExtension {
    Project project

    String sources = null
    String tests = null
    Object reportPath = "${ -> project.buildDir}/test-results/test/TEST-${ -> project.name}.xml"

    NodePluginExtension(Project project) {
      this.project = project;
    }
}

def extension = project.extensions.create('nodeBuild', NodePluginExtension, project)

apply plugin: "com.moowork.node"
node {
  version = '10.0.0'
  npmVersion = '6.0.0'
  download = true
}

npmInstall {
  args = [project.projectDir]
}

task cleanWorkspace(type: Delete) {
  delete "node_modules"
}

task mocha(type: NpmTask, dependsOn: npmInstall) {
  npmCommand = [ 'run', 'test' ]
}
afterEvaluate {
  mocha {
    if (extension.sources) {
      inputs.dir extension.sources
    }
    if (extension.tests) {
      inputs.dir extension.tests
    }

    outputs.file extension.reportPath

    args = [ '--', extension.tests,
         "--reporter", "mocha-junit-reporter",
         "--reporter-options", "mochaFile=${extension.reportPath}" ]
  }
}

task test(dependsOn: mocha)

assemble.dependsOn npmInstall
check.dependsOn test
